package Fire_Wall

import Vectors
import Angle
import Knockback
import TimedLoop

public constant real fire_Wall_CASTING_TIME = 2

class Fire_Wall
	use TimedLoop
	
	private player casterOwner
	private unit caster
	private unit dummy
	private real duration
	
	ondestroy
		this.casterOwner = null
		this.caster = null
		
		this.dummy.kill()
		this.dummy = null
	
	override function onTimedLoop()
		this.duration -= 0.03125
	
		GroupEnumUnitsInRange(bj_lastCreatedGroup, this.dummy.getX(), this.dummy.getY(), 300, null)
		
		for unit enumUnit from bj_lastCreatedGroup
			if ( IsUnitEnemy(enumUnit, this.casterOwner) )
				UnitDamageTarget(this.caster, enumUnit, 4, true, false, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_FIRE, null)
				
		if ( this.duration <= 0 )
			this.stopTimedLoopAndDestroy()
	
	construct(unit caster, vec2 point)
		vec2 casterLoc = vec2(caster.getX(), caster.getY())
		angle a = casterLoc.angleTo(point)
		
		this.casterOwner = caster.getOwner()
		this.caster = caster
		this.dummy = createUnit(this.casterOwner, 'h004', casterLoc.toVec3(), a)
		this.duration = 3
		
		new Knockback(caster, this.dummy, 800, 3, a)
		
		this.startTimedLoop()

function onCast()
	if ( GetSpellAbilityId() == 'A003' )
		vec2 castPoint = vec2(GetSpellTargetX(), GetSpellTargetY())
		new Fire_Wall(GetTriggerUnit(), castPoint)

init
	trigger onCast = CreateTrigger()
	
	onCast.registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT)
	onCast.addAction(function onCast)
	
	onCast = null