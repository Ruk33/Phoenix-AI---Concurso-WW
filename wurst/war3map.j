//===========================================================================
// 
// Wonder Arena Concurso de BOSSES
// 
//   Warcraft III map script
//   Generated by the Warcraft III World Editor
//   Date: Sun Dec 29 15:21:55 2013
//   Map Author: WorldOfEditors
// 
//===========================================================================

//***************************************************************************
//*
//*  Global Variables
//*
//***************************************************************************

globals
    // User-defined
    location                udg_Centro                 = null
    real                    udg_MaxRadio               = 0
    player                  udg_JUGADOR                = null
    player                  udg_JUGBOSS                = null
    hashtable               udg_wwT                    = null
    weathereffect           udg_wwMeteo                = null
    group                   udg_wwG                    = null
    integer                 udg_wwTerreno              = 0
    unit                    udg_Heroe                  = null
    hashtable               udg_wwS                    = null
    hashtable               udg_wwHT                   = null
    hashtable               udg_wwAH                   = null
    location                udg_wwL                    = null
    location                udg_wwLL                   = null
    unit                    udg_wwU                    = null
    integer array           udg_wwProyTipo
    real array              udg_wwProyDir
    real array              udg_wwVelAscendente
    real array              udg_wwVelPlana
    unit array              udg_wwProyectiles
    effect array            udg_wwEfectos
    unit array              udg_wwReferencias
    real array              udg_wwPushBack
    trigger array           udg_wwTipos
    integer                 udg_wwI                    = 0
    integer                 udg_wwFisiCant             = 0
    real                    udg_wwReal                 = 0
    hashtable               udg_wwHashTable            = null
    integer array           udg_MMDashes
    hashtable               udg_wwmmHashTable          = null

    // Generated
    rect                    gg_rct_Region_Central      = null
    rect                    gg_rct_TerrenoPunto        = null
    camerasetup             gg_cam_Normal              = null
    trigger                 gg_trg_INICIO              = null
endglobals

function InitGlobals takes nothing returns nothing
    local integer i = 0
    set udg_MaxRadio = 0
    set udg_JUGADOR = Player(0)
    set udg_JUGBOSS = Player(11)
    set udg_wwG = CreateGroup()
    set udg_wwTerreno = 0
    set i = 0
    loop
        exitwhen (i > 1)
        set udg_wwProyTipo[i] = 0
        set i = i + 1
    endloop

    set i = 0
    loop
        exitwhen (i > 1)
        set udg_wwProyDir[i] = 0
        set i = i + 1
    endloop

    set i = 0
    loop
        exitwhen (i > 1)
        set udg_wwVelAscendente[i] = 0
        set i = i + 1
    endloop

    set i = 0
    loop
        exitwhen (i > 1)
        set udg_wwVelPlana[i] = 0
        set i = i + 1
    endloop

    set i = 0
    loop
        exitwhen (i > 1)
        set udg_wwPushBack[i] = 0
        set i = i + 1
    endloop

    set udg_wwI = 0
    set udg_wwFisiCant = 0
    set udg_wwReal = 0
endfunction

//***************************************************************************
//*
//*  Regions
//*
//***************************************************************************

function CreateRegions takes nothing returns nothing
    local weathereffect we

    set gg_rct_Region_Central = Rect( -256.0, -512.0, 256.0, 0.0 )
    set gg_rct_TerrenoPunto = Rect( -960.0, 576.0, -832.0, 704.0 )
endfunction

//***************************************************************************
//*
//*  Cameras
//*
//***************************************************************************

function CreateCameras takes nothing returns nothing

    set gg_cam_Normal = CreateCameraSetup(  )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_ZOFFSET, 0.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_ROTATION, 90.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_ANGLE_OF_ATTACK, 304.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_TARGET_DISTANCE, 1650.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_ROLL, 0.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_FIELD_OF_VIEW, 70.0, 0.0 )
    call CameraSetupSetField( gg_cam_Normal, CAMERA_FIELD_FARZ, 5000.0, 0.0 )
    call CameraSetupSetDestPosition( gg_cam_Normal, -9.6, -256.1, 0.0 )

endfunction

//***************************************************************************
//*
//*  Custom Script Code
//*
//***************************************************************************
//TESH.scrollpos=849
//TESH.alwaysfold=0
//CONCURSO DE BOSSES PARA WORDLEDIT.NET
//===========================================================================
//Funciones Auxiliares
function wwMod takes real a, real b returns real
    return SquareRoot(a*a + b*b)
endfunction
//===========================================================================

//Spells

//Spell Dum 2
function SPDum2con takes nothing returns boolean
    return GetSpellAbilityId() == 'AAD2'
endfunction

function SPDum2acc takes nothing returns nothing
    local unit u = GetTriggerUnit()
    local effect ef = AddSpecialEffectTarget("Abilities\\Spells\\Orc\\AncestralSpirit\\AncestralSpiritCaster.mdl", u, "overhead")
    call TriggerSleepAction(1.0)
    call DestroyEffect(ef)
    set ef = null
    set u = null
endfunction

function SPDum2ini takes nothing returns nothing
    local trigger t = CreateTrigger(  )
    call TriggerRegisterAnyUnitEventBJ( t, EVENT_PLAYER_UNIT_SPELL_CHANNEL )
    call TriggerAddCondition( t, Condition( function SPDum2con ) )
    call TriggerAddAction( t, function SPDum2acc )
    call Preload("Abilities\\Spells\\Orc\\AncestralSpirit\\AncestralSpiritCaster.mdl")
    set t = null
endfunction

//Spell 1
function wwDispose takes nothing returns nothing
    call DestroyEffect(udg_wwEfectos[udg_wwI])
    //--------
    set udg_wwProyTipo[udg_wwI] = udg_wwProyTipo[udg_wwFisiCant]
    set udg_wwProyDir[udg_wwI] = udg_wwProyDir[udg_wwFisiCant]
    set udg_wwVelAscendente[udg_wwI] = udg_wwVelAscendente[udg_wwFisiCant]
    set udg_wwVelPlana[udg_wwI] = udg_wwVelPlana[udg_wwFisiCant]
    set udg_wwProyectiles[udg_wwI] = udg_wwProyectiles[udg_wwFisiCant]
    set udg_wwReferencias[udg_wwI] = udg_wwReferencias[udg_wwFisiCant]
    set udg_wwPushBack[udg_wwI] = udg_wwPushBack[udg_wwFisiCant]
    set udg_wwEfectos[udg_wwI] = udg_wwEfectos[udg_wwFisiCant]
    // Nulear
    set udg_wwEfectos[udg_wwFisiCant] = null
    set udg_wwProyTipo[udg_wwFisiCant] = 0
    set udg_wwProyDir[udg_wwFisiCant] = 0.00
    set udg_wwVelAscendente[udg_wwFisiCant] = 0.00
    set udg_wwVelPlana[udg_wwFisiCant] = 0.00
    set udg_wwProyectiles[udg_wwFisiCant] = null
    set udg_wwReferencias[udg_wwFisiCant] = null
    set udg_wwPushBack[udg_wwFisiCant] = 0.00
    // -
    set udg_wwFisiCant = udg_wwFisiCant - 1
endfunction

function wwFinalizarProyectil takes nothing returns boolean
    call KillUnit(udg_wwProyectiles[udg_wwI])
    call wwDispose()
    set udg_wwI = udg_wwI - 1
    return false
endfunction

function wwFiltroWG1 takes nothing returns boolean
    return GetUnitState(GetFilterUnit(), UNIT_STATE_LIFE) > 0 and IsUnitType(GetFilterUnit(), UNIT_TYPE_GROUND) == true and GetUnitAbilityLevel(GetFilterUnit(), 'Bvul') == 0
endfunction

function wwFor1Group takes nothing returns nothing
    local real x = GetUnitX(udg_wwProyectiles[udg_wwI])
    local real y = GetUnitY(udg_wwProyectiles[udg_wwI])
    local real radio = RMinBJ(RMaxBJ(I2R(GetUnitPointValue(GetEnumUnit())),50.0),200.0)
    if wwMod(x-GetUnitX(GetEnumUnit()), y-GetUnitY(GetEnumUnit())) <= radio/3.7 + 37.50 then
        if GetEnumUnit() == udg_wwReferencias[udg_wwI] then
            if udg_wwVelPlana[udg_wwI] >= 10.00 then
                set udg_wwProyTipo[udg_wwI] = 0
                call UnitRemoveAbility(udg_wwReferencias[udg_wwI], 'AAH1')
                call UnitAddAbility(udg_wwReferencias[udg_wwI], 'AAH1')
                call SetUnitState(udg_wwReferencias[udg_wwI], UNIT_STATE_MANA, RMinBJ(GetUnitState(udg_wwReferencias[udg_wwI], UNIT_STATE_MANA)+65.0,GetUnitState(udg_wwReferencias[udg_wwI], UNIT_STATE_MAX_MANA)))
            else
            endif
        elseif GetOwningPlayer(GetEnumUnit()) != GetOwningPlayer(udg_wwProyectiles[udg_wwI]) then
            set udg_wwFisiCant = udg_wwFisiCant + 1
            set udg_wwProyDir[udg_wwFisiCant] = udg_wwProyDir[udg_wwI]
            set udg_wwProyectiles[udg_wwFisiCant] = GetEnumUnit()
            set udg_wwProyTipo[udg_wwFisiCant] = 2
            set udg_wwVelAscendente[udg_wwFisiCant] = 0.80 + (udg_wwReal * 0.10)
            set udg_wwVelPlana[udg_wwFisiCant] = 90.0*(udg_wwPushBack[udg_wwI] + (udg_wwReal * 0.15))/(Pow(0.07*radio,2)+50.0)
            set udg_wwEfectos[udg_wwFisiCant] = AddSpecialEffectTarget("Objects\\Spawnmodels\\Human\\HumanBlood\\HumanBloodLarge0.mdl", udg_wwProyectiles[udg_wwFisiCant], "chest")
            call UnitDamageTarget(udg_wwProyectiles[udg_wwI], GetEnumUnit(), 0.85*udg_wwReal, true, false, ATTACK_TYPE_MELEE, DAMAGE_TYPE_NORMAL, WEAPON_TYPE_WHOKNOWS)
        endif
    endif
endfunction

function wwBoomerang takes nothing returns boolean
    local real x = GetUnitX(udg_wwProyectiles[udg_wwI])
    local real y = GetUnitY(udg_wwProyectiles[udg_wwI])
    local real radio = 91.60
    if RAbsBJ(udg_wwVelPlana[udg_wwI]) >= 6.00 then
        set udg_wwReal = RAbsBJ(udg_wwVelPlana[udg_wwI])
    else
        set udg_wwReal = 6.00
    endif
    if wwMod(x-GetLocationX(udg_Centro), y-GetLocationY(udg_Centro)) > udg_MaxRadio-20.0 then
        call KillUnit(udg_wwProyectiles[udg_wwI])
        call wwDispose()
        set udg_wwI = udg_wwI - 1
    else
        set x = x + CosBJ(udg_wwProyDir[udg_wwI])*udg_wwReal
        set y = y + SinBJ(udg_wwProyDir[udg_wwI])*udg_wwReal
        if udg_wwVelPlana[udg_wwI] < 75.00 then
            set udg_wwVelPlana[udg_wwI] = udg_wwVelPlana[udg_wwI] + 0.9
            set udg_wwProyDir[udg_wwI] = udg_wwProyDir[udg_wwI] + 3.07//2.95
            call SetUnitPosition(udg_wwProyectiles[udg_wwI], x, y)
            call GroupEnumUnitsInRange(udg_wwG, x, y, radio, Condition(function wwFiltroWG1))
            call ForGroup(udg_wwG, function wwFor1Group)
        else
            call KillUnit(udg_wwProyectiles[udg_wwI])
            call wwDispose()
            set udg_wwI = udg_wwI - 1
        endif
    endif
    return false
endfunction

function wwEmpujar takes nothing returns boolean
    local real x = GetUnitX(udg_wwProyectiles[udg_wwI]) + CosBJ(udg_wwProyDir[udg_wwI])*udg_wwVelPlana[udg_wwI]
    local real y = GetUnitY(udg_wwProyectiles[udg_wwI]) + SinBJ(udg_wwProyDir[udg_wwI])*udg_wwVelPlana[udg_wwI]
    if udg_wwVelPlana[udg_wwI] > 0.00 then
        set udg_wwVelPlana[udg_wwI] = udg_wwVelPlana[udg_wwI] - udg_wwVelAscendente[udg_wwI]
        call SetUnitX(udg_wwProyectiles[udg_wwI], x)
        call SetUnitY(udg_wwProyectiles[udg_wwI], y)
    else
        call wwDispose()
        set udg_wwI = udg_wwI - 1
    endif
    return false
endfunction

function wwWGfisica takes nothing returns nothing
    local integer i = 0
    local integer iMAX = udg_wwFisiCant
    set udg_wwI = 0
    loop
        exitwhen i >= iMAX 
        set udg_wwI = udg_wwI + 1
        call TriggerEvaluate(udg_wwTipos[udg_wwProyTipo[udg_wwI]])
        set i = i + 1
    endloop
endfunction

function WonderGlaive_con takes nothing returns boolean
    return GetSpellAbilityId() == 'AAH1'
endfunction

function WonderGlaive_acc takes nothing returns nothing
    local unit u = GetSpellAbilityUnit()
    local unit dum
    local player jug = GetOwningPlayer(u)
    local real x = GetUnitX(u)
    local real y = GetUnitY(u)
    local real angulo = Atan2(GetSpellTargetY()-y, GetSpellTargetX()-x)*bj_RADTODEG-32.0
    set udg_wwFisiCant = udg_wwFisiCant + 1
    set dum = CreateUnit(jug, 'e002', x, y, angulo)
    call UnitAddAbility(dum, 'Arav')
    call UnitRemoveAbility(dum, 'Arav')
    call SetUnitPathing(dum, false)
    set udg_wwReferencias[udg_wwFisiCant] = u
    set udg_wwProyectiles[udg_wwFisiCant] = dum
    set udg_wwProyDir[udg_wwFisiCant] = angulo
    set udg_wwProyTipo[udg_wwFisiCant] = 1
    set udg_wwPushBack[udg_wwFisiCant] = 4.00
    set udg_wwVelAscendente[udg_wwFisiCant] = 0.00
    set udg_wwVelPlana[udg_wwFisiCant] = -40.00
    call SetUnitFlyHeight(dum, 60.00, 0.00 )
    set udg_wwEfectos[udg_wwFisiCant] = AddSpecialEffectTarget("Abilities\\Weapons\\ZigguratMissile\\ZigguratMissile.mdl", dum, "origin")
    set u = null
    set dum = null
    set jug = null
endfunction

function SP1ini takes nothing returns nothing
    local trigger t = CreateTrigger()
    local timer tim = CreateTimer()
    call TriggerRegisterAnyUnitEventBJ(t, EVENT_PLAYER_UNIT_SPELL_EFFECT )
    call TriggerAddCondition(t, Condition(function WonderGlaive_con))
    call TriggerAddAction(t, function WonderGlaive_acc)
    set t = null
    call Preload("Objects\\Spawnmodels\\Human\\HumanBlood\\HumanBloodLarge0.mdl")
    call Preload("Abilities\\Weapons\\ZigguratMissile\\ZigguratMissile.mdl")
    set udg_wwTipos[0] = CreateTrigger()
    call TriggerAddCondition(udg_wwTipos[0], Condition(function wwFinalizarProyectil))
    set udg_wwTipos[1] = CreateTrigger()
    call TriggerAddCondition(udg_wwTipos[1], Condition(function wwBoomerang))
    set udg_wwTipos[2] = CreateTrigger()
    call TriggerAddCondition(udg_wwTipos[2], Condition(function wwEmpujar))
    call TimerStart(tim, 0.05, true, function wwWGfisica)
endfunction

//Spell 2
//*Angelos*
//library LibraryLoad

//Load - Save (Integer) 
function SetHandleInt takes handle subject, string name, integer value returns nothing
  call SaveInteger(udg_wwHashTable, GetHandleId(subject), StringHash(name), value)
endfunction

function GetHandleInt takes handle subject, string name returns integer
  return LoadInteger(udg_wwHashTable, GetHandleId(subject), StringHash(name))
endfunction

function RemoveInt takes handle subject, string name returns nothing
  call SaveInteger(udg_wwHashTable,GetHandleId(subject),StringHash(name), 0)
  call RemoveSavedInteger(udg_wwHashTable,GetHandleId(subject),StringHash(name))
endfunction

//Load - Save (Real) 
function SetHandleReal takes handle subject, string name, real value returns nothing
  call SaveReal(udg_wwHashTable, GetHandleId(subject), StringHash(name), value)
endfunction

function GetHandleReal takes handle subject, string name returns real
  return LoadReal(udg_wwHashTable, GetHandleId(subject), StringHash(name))
endfunction

function RemoveReal takes handle subject, string name returns nothing
  call SaveReal(udg_wwHashTable,GetHandleId(subject),StringHash(name), 0)
  call RemoveSavedReal(udg_wwHashTable,GetHandleId(subject),StringHash(name))
endfunction

//Load - Save (Boolean) 
function SetHandleBoolean takes handle subject, string name, boolean value returns nothing
  call SaveBoolean(udg_wwHashTable, GetHandleId(subject), StringHash(name), value)
endfunction

function GetHandleBoolean takes handle subject, string name returns boolean
  return LoadBoolean(udg_wwHashTable, GetHandleId(subject), StringHash(name))
endfunction

function RemoveBoolean takes handle subject, string name returns nothing
  call SaveBoolean(udg_wwHashTable,GetHandleId(subject),StringHash(name), false)
  call RemoveSavedBoolean(udg_wwHashTable,GetHandleId(subject),StringHash(name))
endfunction

//Load - Save (Unit) 
function SetHandleUnit takes handle subject, string name, unit value returns nothing
  call SaveUnitHandle(udg_wwHashTable, GetHandleId(subject), StringHash(name), value)
endfunction

function GetHandleUnit takes handle subject, string name returns unit
  return LoadUnitHandle(udg_wwHashTable, GetHandleId(subject), StringHash(name))
endfunction

function RemoveHandleUnit takes handle subject, string name returns nothing
  call SaveUnitHandle(udg_wwHashTable,GetHandleId(subject),StringHash(name), null)
  call RemoveSavedHandle(udg_wwHashTable,GetHandleId(subject),StringHash(name))
endfunction

//Load - Save (Handle) 

function SetHandleHandle takes handle subject, string name, agent value returns nothing
  call SaveAgentHandle(udg_wwHashTable, GetHandleId(subject), StringHash(name), value)
endfunction

function GetHandleTrigger takes handle subject, string name returns trigger
  return LoadTriggerHandle(udg_wwHashTable, GetHandleId(subject), StringHash(name))
endfunction

function RemoveHandle takes handle subject, string name returns nothing
  call SaveAgentHandle(udg_wwHashTable,GetHandleId(subject),StringHash(name), null)
  call RemoveSavedHandle(udg_wwHashTable,GetHandleId(subject),StringHash(name))
endfunction

function FlushHandleLocals takes handle subject returns nothing
 call FlushChildHashtable(udg_wwHashTable, GetHandleId(subject) )
endfunction

//endlibrary
//*Angelos*


// |-------------|
// | Constants:  |
// |-------------|
    function GradesOnly360 takes real ang returns real
        loop
            exitwhen ang >= 0 and ang <= 360
            if ang > 360 then
                set ang = ang - 360
            elseif ang < 0 then
                set ang = ang + 360
            endif
        endloop
        return ang
    endfunction
    
// |------------------|
// | End of Constants |
// |------------------|

    function Life takes nothing returns nothing
        local timer time = GetExpiredTimer()
        local unit u = GetHandleUnit(time,"Defensa_Radius_unit")
        local real life = GetHandleReal(time,"Defensa_Radius_life")
        call SetWidgetLife(u,GetWidgetLife(u)+life)
        call RemoveHandleUnit(time,"Defensa_Radius_unit")
        call RemoveReal(time,"Defensa_Radius_life")
        call PauseTimer(time) //Pausamos el timer
        call DestroyTimer(time) //Destruimos el timer
        set time = null
        set u = null
    endfunction
    
    function Defensa_Radius_Act takes nothing returns boolean
        local trigger t = GetTriggeringTrigger()
        local unit u = GetTriggerUnit()
        local unit targ = GetEventDamageSource()
        local real dam = GetEventDamage()
        local real x = GetUnitX(u)
        local real y = GetUnitY(u)
        local real xt
        local real yt
        local real ang = GetUnitFacing(u)
        local real angle
        local real admod1
        local real admod2
        local timer time
        local boolean b = false
        local boolean c = false
        
        if IsPlayerEnemy(GetOwningPlayer(u),GetOwningPlayer(targ)) and GetHandleBoolean(u,"Defensa_Radius_Active") and (dam>.1) then
            set xt = GetUnitX(targ)
            set yt = GetUnitY(targ)
            set angle = ModuloReal(Atan2(yt-y,xt-x)*bj_RADTODEG,360)
            set admod1 = ang - 90/2
            set admod2 = ang + 90/2
            if admod1 < 0 or admod2 > 360 then
                set admod1 = GradesOnly360(admod1)
                set admod2 = GradesOnly360(admod2)
                if angle >= admod1 or angle <= admod2 then
                    set dam = dam*0.5
                    set b = true
                    set c = true
                endif
            elseif angle >= admod1 and angle <= admod2 then
                set dam = dam*0.5
                set b = true
                set c = true
            endif
            
            if b == false then
                set admod1 = ang - 270/2
                set admod2 = ang + 270/2
                if admod1 < 0 or admod2 > 360 then
                    set admod1 = GradesOnly360(admod1)
                    set admod2 = GradesOnly360(admod2)
                    if angle >= admod1 or angle <= admod2 then
                        set dam = dam*0.25
                        set b = true
                    endif
                elseif angle >= admod1 and angle <= admod2 then
                    set dam = dam*0.25
                    set b = true
                endif
            endif
        endif
        
        if b then
            if c == true then
                call DestroyEffect(AddSpecialEffectTarget("Abilities\\Spells\\Human\\Defend\\DefendCaster.mdl",u,"origin"))
            else
                call DestroyEffect(AddSpecialEffectTarget("Abilities\\Spells\\Human\\FlakCannons\\FlakTarget.mdl",u,"origin"))
            endif
            set time = CreateTimer()
            call SetHandleUnit(time,"Defensa_Radius_unit",u)
            call SetHandleReal(time,"Defensa_Radius_life",dam)
            call TimerStart(time,0.,false,function Life)
            set time = null
        endif
        
        set t = null
        set targ = null
        set u = null
        return false
    endfunction

    function Defensa_Radius takes nothing returns nothing
        local unit u = GetTriggerUnit()
        local trigger t
        if GetTriggerEventId() == EVENT_PLAYER_UNIT_ISSUED_ORDER then
            if GetHandleBoolean(u,"Defensa_Radius") == false and OrderId2String(GetIssuedOrderId()) == "defend" then
                set t = CreateTrigger()
                //Guardo mis valores dentro del timer
                call SetHandleBoolean(u,"Defensa_Radius",true)
                call SetHandleBoolean(u,"Defensa_Radius_Active",true)
                call SetHandleHandle(u,"Defensa_Radius_trigger",t)
                call TriggerRegisterUnitEvent(t,u,EVENT_UNIT_DAMAGED)
                call TriggerAddCondition(t,Condition(function Defensa_Radius_Act))
                set t = null
            elseif OrderId2String(GetIssuedOrderId()) == "defend" and GetHandleBoolean(u,"Defensa_Radius") == true then
                call SetHandleBoolean(u,"Defensa_Radius_Active",true)
            elseif OrderId2String(GetIssuedOrderId()) == "undefend" and GetHandleBoolean(u,"Defensa_Radius") == true then
                call SetHandleBoolean(u,"Defensa_Radius_Active",false)
            endif
        elseif GetTriggerEventId() == EVENT_PLAYER_UNIT_DEATH and GetHandleBoolean(u,"Defensa_Radius") == true then
            call DestroyTrigger(GetHandleTrigger(u,"Defensa_Radius_trigger"))
            call RemoveBoolean(u,"Defensa_Radius")
            call RemoveBoolean(u,"Defensa_Radius_Active")
            call RemoveHandle(u,"Defensa_Radius_trigger")
        endif
        set u = null
    endfunction

//===========================================================================
function SP2ini takes nothing returns nothing
    local trigger t = CreateTrigger()//El trigger
    call TriggerRegisterAnyUnitEventBJ(t, EVENT_PLAYER_UNIT_ISSUED_ORDER)
    call TriggerRegisterAnyUnitEventBJ(t, EVENT_PLAYER_UNIT_DEATH)
    call TriggerAddAction(t, function Defensa_Radius)
    call Preload("Abilities\\Spells\\Human\\FlakCannons\\FlakTarget.mdl")
    call Preload("Abilities\\Spells\\Human\\Defend\\DefendCaster.mdl")
    set t = null
    set udg_wwHashTable = InitHashtable()
endfunction

//Spell 3
function Dash_CD takes nothing returns nothing
    local timer t = GetExpiredTimer()
    local integer id = GetHandleId(t)
    local unit u = LoadUnitHandle(udg_wwmmHashTable, id, 0)
    local integer Id = GetHandleId(u)
    local integer charges = LoadInteger(udg_wwmmHashTable, Id, StringHash("Charges")) - 1
    
    if GetUnitAbilityLevel(u, udg_MMDashes[1]) > 0 then
        call UnitAddAbility(u, udg_MMDashes[0])
        call UnitRemoveAbility(u, udg_MMDashes[1])
    endif
    
    if charges != 0 then
        call TimerStart(t, 8, true, function Dash_CD)
    else
        call SaveTimerHandle(udg_wwmmHashTable, Id, 0, null)
        call RemoveSavedHandle(udg_wwmmHashTable, Id, 0)
        call FlushChildHashtable(udg_wwmmHashTable, id)
        call DestroyTimer(t)
    endif
    
    call SaveInteger(udg_wwmmHashTable, Id, StringHash("Charges"), charges)
    
    set t = null
    set u = null
    
endfunction

function Dash_Timer takes nothing returns nothing
    local timer t = GetExpiredTimer()
    local integer Id = GetHandleId(t)
    local unit u = LoadUnitHandle(udg_wwmmHashTable, Id, 0)
    local integer id = GetHandleId(u)
    local real s = LoadReal(udg_wwmmHashTable, id, 1)
    local real a = LoadReal(udg_wwmmHashTable, id, 2)
    local real time = LoadReal(udg_wwmmHashTable, id, 3) + 0.04
    local real x = LoadReal(udg_wwmmHashTable, id, 4)
    local real y = LoadReal(udg_wwmmHashTable, id, 5)
    local real nx = x + s * Cos(a)
    local real ny = y + s * Sin(a)
    local real xCen = GetLocationX(udg_Centro)
    local real yCen = GetLocationY(udg_Centro)
    local real distancia
    if time <= 0.5 then
        set distancia = wwMod(nx-xCen, ny-yCen)
        if distancia > 830.0 then
            set nx = xCen + 830.0*(nx-xCen)/distancia
            set ny = yCen + 830.0*(ny-yCen)/distancia
        endif
        call SetUnitX(u,nx)
        call SetUnitY(u,ny)
        
        call SaveReal(udg_wwmmHashTable, id, 3, time)
        call SaveReal(udg_wwmmHashTable, id, 4, nx)
        call SaveReal(udg_wwmmHashTable, id, 5, ny)
    else
        //call SetUnitTimeScale(u, 1)
        call PauseTimer(t)
        call SaveTimerHandle(udg_wwmmHashTable, id, 0, null)
        call RemoveSavedHandle(udg_wwmmHashTable, id, 0)
        call FlushChildHashtable(udg_wwmmHashTable, Id)
        call DestroyTimer(t)
    endif
    
    set t = null
    set u = null
        
endfunction

function Dash_Conditions takes nothing returns boolean
    return GetIssuedOrderId() == OrderId("evileye")
endfunction

function Dash_Actions takes nothing returns nothing
    local unit u = GetTriggerUnit()
    local integer id = GetHandleId(u)
    local timer t = LoadTimerHandle(udg_wwmmHashTable, id, 0)
    local real xt = GetOrderPointX()
    local real yt = GetOrderPointY()
    local real x = GetUnitX(u)
    local real y = GetUnitY(u)
    local real dx = xt - x
    local real dy = yt - y
    local real a = Atan2(dy, dx)
    local real d = SquareRoot(dx*dx + dy*dy)
    local integer charges = LoadInteger(udg_wwmmHashTable, id, StringHash("Charges")) + 1
    local texttag text = CreateTextTagUnitBJ( ( I2S(3-charges) + "!"), u, 0, 10, 100, 100, 100, 0 )
   
    call SetTextTagVelocityBJ( text, 32.00, 90 )
    call SetTextTagPermanent( text, false )
    call SetTextTagLifespan( text, 3.00 )
    
    call IssueImmediateOrder(u, "stop")
    //call SetUnitTimeScale(u, 3)
    call SetUnitAnimation(u,"spell one")
    call DestroyEffect(AddSpecialEffectTarget("Abilities\\Spells\\Human\\Invisibility\\InvisibilityTarget.mdl", u, "origin"))
    
    call SetUnitState(u, UNIT_STATE_MANA, GetUnitState(u,UNIT_STATE_MANA) - 70.0)
    
    if t == null then
        set t = CreateTimer()
        call SaveTimerHandle(udg_wwmmHashTable, id, 0, t)
        call SaveUnitHandle(udg_wwmmHashTable, GetHandleId(t), 0, u)
    endif
    
    if d > 500.00 then
        set xt = x + 500 * Cos(a)
        set yt = y + 500 * Sin(a)
        set d = 500
    endif
    
    call SaveReal(udg_wwmmHashTable, id, 1, d / 0.5 / 25) // Velocidad por perÃ­odo
    call SaveReal(udg_wwmmHashTable, id, 2, a)
    call SaveReal(udg_wwmmHashTable, id, 3, 0.00)
    call SaveReal(udg_wwmmHashTable, id, 4, x)
    call SaveReal(udg_wwmmHashTable, id, 5, y)
    call SaveInteger(udg_wwmmHashTable, id, StringHash("Charges"), charges)
    
    call TimerStart(t, 0.03125, true, function Dash_Timer)
    
    set t = LoadTimerHandle(udg_wwmmHashTable, id, -1)
    if t == null then
        set t = CreateTimer()
        call SaveTimerHandle(udg_wwmmHashTable, id, -1, t)
        call SaveUnitHandle(udg_wwmmHashTable, GetHandleId(t), 0, u)
    endif
    
    if charges == 3 then
        call UnitRemoveAbility(u, udg_MMDashes[0])
        call UnitAddAbility(u, udg_MMDashes[1])
    endif
    
    call TimerStart(t, 8, false, function Dash_CD)
    
    set t = null
    set u = null
    set text = null
    
endfunction

//===========================================================================
function SP3ini takes nothing returns nothing
    local trigger t = CreateTrigger(  )
    call TriggerRegisterAnyUnitEventBJ( t, EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER )
    call TriggerAddCondition( t, Condition( function Dash_Conditions ) )
    call TriggerAddAction( t, function Dash_Actions )
    set t = null
    set udg_MMDashes[0] = 'AAH3'
    set udg_MMDashes[1] = 'AAD4'
    set udg_wwmmHashTable = InitHashtable()
    call Preload("Abilities\\Spells\\Human\\Invisibility\\InvisibilityTarget.mdl")
endfunction

//Funciones de Terreno
function wwRellenarFila takes integer fila, integer terreno, integer c1, integer c2, integer c3, integer c4, integer c5, integer c6, integer c7, integer c8, integer c9, integer c10, integer c11, integer c12, integer c13, integer c14, integer c15 returns nothing
    local integer filareal = terreno * 15 + fila
    call SaveInteger(udg_wwT, filareal,  1,  c1)
    call SaveInteger(udg_wwT, filareal,  2,  c2)
    call SaveInteger(udg_wwT, filareal,  3,  c3)
    call SaveInteger(udg_wwT, filareal,  4,  c4)
    call SaveInteger(udg_wwT, filareal,  5,  c5)
    call SaveInteger(udg_wwT, filareal,  6,  c6)
    call SaveInteger(udg_wwT, filareal,  7,  c7)
    call SaveInteger(udg_wwT, filareal,  8,  c8)
    call SaveInteger(udg_wwT, filareal,  9,  c9)
    call SaveInteger(udg_wwT, filareal, 10, c10)
    call SaveInteger(udg_wwT, filareal, 11, c11)
    call SaveInteger(udg_wwT, filareal, 12, c12)
    call SaveInteger(udg_wwT, filareal, 13, c13)
    call SaveInteger(udg_wwT, filareal, 14, c14)
    call SaveInteger(udg_wwT, filareal, 15, c15)
endfunction

function wwTerrenos takes nothing returns nothing
    local integer X0 = 0
    local integer t1
    local integer t2
    local integer t3
    local integer t4
    local integer t5
    local integer t6
    local integer t7
    local integer t8
    local integer t9
    local integer ter
    
    //TERRENO 0 (ARENA)
    set t1 = 'Zdrt' //Tierra Ruinas sumergidas
    set t2 = 'Zdtr' //Tierra agreste Ruinas sumergidas
    set t3 = 'Zsan' //Arena Ruinas sumergidas
    set ter = 0
    call SaveInteger(udg_wwT, 0, ter, 'LRaa') //Rayos de luz
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t2,t2,t2,t2,t1,t1,t3,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t2,t2,t2,t2,t2,t2,t1,t1,t3,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t2,t2,t3,t2,t2,t2,t2,t3,t1,t1,t3,X0,X0)
    call wwRellenarFila( 5, ter, X0,t2,t3,t2,t2,t2,t2,t2,t2,t2,t2,t1,t3,t2,X0)
    call wwRellenarFila( 6, ter, X0,t2,t2,t2,t2,t2,t1,t2,t2,t2,t2,t1,t2,t2,X0)
    call wwRellenarFila( 7, ter, X0,t2,t2,t2,t2,t1,t2,t2,t2,t2,t2,t2,t2,t2,X0)
    call wwRellenarFila( 8, ter, t3,t2,t2,t3,t2,t2,t3,t2,t1,t2,t2,t2,t2,t2,t2)
    call wwRellenarFila( 9, ter, X0,t2,t2,t3,t1,t3,t3,t3,t3,t2,t2,t3,t3,t2,X0)
    call wwRellenarFila(10, ter, X0,t1,t2,t2,t3,t3,t3,t1,t3,t2,t2,t2,t2,t2,X0)
    call wwRellenarFila(11, ter, X0,t1,t3,t2,t2,t1,t1,t1,t2,t2,t2,t2,t2,t3,X0)
    call wwRellenarFila(12, ter, X0,X0,t2,t2,t2,t2,t3,t3,t2,t2,t1,t1,t2,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t3,t2,t2,t2,t2,t2,t2,t1,t1,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t3,t2,t2,t2,t2,t2,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    
    //TERRENO 1 (HIERBA)
    set t1 = 'Agrs' //Hierba Ashenvale
    set t2 = 'Zdrg' //Tierra con hierba Ruinas sumergidas
    set t3 = 'Agrd' //Hierba desigual Ashenvale
    set ter = 1
    call SaveInteger(udg_wwT, 0, ter, 'RLlr') //Lluvia ligera
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t2,t2,t2,t3,t2,t1,t3,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t2,t2,t2,t3,t3,t2,t1,t1,t3,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t3,t2,t3,t2,t1,t1,t3,t3,t1,t1,t3,X0,X0)
    call wwRellenarFila( 5, ter, X0,t2,t3,t2,t2,t1,t2,t1,t1,t1,t2,t1,t3,t2,X0)
    call wwRellenarFila( 6, ter, X0,t1,t1,t2,t1,t2,t1,t2,t1,t2,t2,t1,t2,t2,X0)
    call wwRellenarFila( 7, ter, X0,t1,t2,t2,t2,t1,t2,t2,t2,t3,t3,t2,t2,t2,X0)
    call wwRellenarFila( 8, ter, t3,t2,t2,t3,t2,t2,t3,t3,t1,t2,t2,t2,t1,t3,t3)
    call wwRellenarFila( 9, ter, X0,t2,t2,t3,t1,t3,t2,t3,t3,t2,t2,t3,t3,t3,X0)
    call wwRellenarFila(10, ter, X0,t1,t2,t2,t3,t3,t3,t1,t3,t2,t1,t2,t2,t2,X0)
    call wwRellenarFila(11, ter, X0,t1,t3,t2,t2,t1,t2,t1,t1,t3,t3,t2,t2,t3,X0)
    call wwRellenarFila(12, ter, X0,X0,t2,t2,t2,t2,t3,t3,t2,t2,t1,t1,t3,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t3,t3,t2,t2,t2,t2,t2,t1,t1,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t3,t2,t2,t1,t3,t2,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    
    //TERRENO 2 (CORONA DE HIELO)
    set t1 = 'Idki' //Hielo oscuro Corona de hielo
    set t2 = 'Nsnr' //Nieve rocosa Northrend
    set t3 = 'Iice' //Hielo Corona de hielo
    set t4 = 'Nsnw' //Nieve Northrend
    set ter = 2
    call SaveInteger(udg_wwT, 0, ter, 'SNls') //Nevada ligera
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t4,t2,t2,t4,t1,t1,t3,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t2,t2,t4,t4,t2,t4,t1,t1,t3,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t2,t2,t3,t2,t2,t2,t2,t3,t1,t1,t3,X0,X0)
    call wwRellenarFila( 5, ter, X0,t2,t3,t2,t2,t4,t2,t2,t4,t2,t2,t1,t3,t2,X0)
    call wwRellenarFila( 6, ter, X0,t4,t2,t4,t2,t2,t1,t4,t4,t2,t2,t1,t2,t2,X0)
    call wwRellenarFila( 7, ter, X0,t4,t4,t2,t2,t1,t2,t2,t4,t2,t2,t2,t2,t4,X0)
    call wwRellenarFila( 8, ter, t3,t2,t4,t3,t2,t4,t3,t2,t1,t2,t2,t2,t4,t2,t2)
    call wwRellenarFila( 9, ter, X0,t4,t4,t3,t1,t3,t3,t3,t3,t2,t2,t3,t3,t4,X0)
    call wwRellenarFila(10, ter, X0,t1,t4,t2,t3,t3,t3,t1,t3,t4,t2,t2,t4,t2,X0)
    call wwRellenarFila(11, ter, X0,t1,t3,t4,t2,t1,t1,t1,t2,t2,t2,t2,t2,t3,X0)
    call wwRellenarFila(12, ter, X0,X0,t2,t2,t4,t4,t3,t3,t2,t4,t1,t1,t2,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t3,t2,t2,t4,t2,t4,t4,t1,t1,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t3,t2,t4,t2,t2,t2,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t1,X0,X0,X0,X0,X0,X0,X0)
    
    //TERRENO 3 (INFIERNO)    
    set t1 = 'Ddrt'//1264874100
    set t2 = 'Kfsl'//1265005420
    set t3 = 'Kdkt'//1264872308
    set t4 = 'Dlvc'//1147958883
    set t5 = 'Ddkr'//1147431794
    set t6 = 'Dgrs'//1147630195
    set ter = 3
    call SaveInteger(udg_wwT, 0, ter, 'FDrh') //Niebla roja
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15    
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t6,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t6,t6,t6,t6,t6,t6,t4,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t6,t6,t5,t5,t5,t5,t5,t6,t4,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t6,t6,t4,t5,t4,t4,t1,t5,t5,t6,t6,X0,X0)
    call wwRellenarFila( 5, ter, X0,t6,t6,t5,t5,t2,t2,t4,t4,t4,t5,t5,t6,t6,X0)
    call wwRellenarFila( 6, ter, X0,t6,t5,t2,t1,t4,t4,t1,t1,t1,t4,t5,t4,t6,X0)
    call wwRellenarFila( 7, ter, X0,t6,t2,t5,t4,t4,t1,t3,t1,t2,t1,t5,t5,t6,X0)
    call wwRellenarFila( 8, ter, t6,t6,t5,t5,t4,t1,t3,t4,t3,t1,t1,t1,t5,t6,t6)
    call wwRellenarFila( 9, ter, X0,t6,t5,t2,t4,t4,t2,t3,t2,t1,t4,t5,t5,t6,X0)
    call wwRellenarFila(10, ter, X0,t6,t5,t5,t1,t2,t4,t1,t4,t4,t4,t5,t5,t1,X0)
    call wwRellenarFila(11, ter, X0,t6,t6,t5,t5,t1,t4,t1,t4,t4,t5,t5,t6,t1,X0)
    call wwRellenarFila(12, ter, X0,X0,t6,t6,t4,t4,t5,t5,t5,t5,t1,t6,t1,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t6,t6,t5,t5,t5,t5,t5,t6,t6,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t6,t6,t6,t6,t6,t6,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t6,X0,X0,X0,X0,X0,X0,X0)
    
    //TERRENO 4 (VENENO)   
    set t1 = 'Cgrs'//hierba
    set t2 = 'Crck'//roca
    set t3 = 'Cgrs'// 
    set t4 = 'Cvin'//vides
    set t5 = 'Clvg'//hojas
    set t6 = 'Cpos'//veneno
    set ter = 4
    call SaveInteger(udg_wwT, 0, ter, 'FDgh') //Niebla roja
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15    
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t6,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t6,t6,t6,t6,t6,t6,t4,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t6,t6,t5,t5,t5,t5,t5,t6,t4,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t6,t6,t4,t5,t4,t4,t1,t5,t5,t6,t6,X0,X0)
    call wwRellenarFila( 5, ter, X0,t6,t6,t5,t5,t2,t2,t4,t4,t4,t5,t5,t6,t6,X0)
    call wwRellenarFila( 6, ter, X0,t6,t5,t2,t1,t4,t4,t1,t1,t1,t4,t5,t4,t6,X0)
    call wwRellenarFila( 7, ter, X0,t6,t2,t5,t4,t4,t1,t3,t1,t2,t1,t5,t5,t6,X0)
    call wwRellenarFila( 8, ter, t6,t6,t5,t5,t4,t1,t3,t4,t3,t1,t1,t1,t5,t6,t6)
    call wwRellenarFila( 9, ter, X0,t6,t5,t2,t4,t4,t2,t3,t2,t1,t4,t5,t5,t6,X0)
    call wwRellenarFila(10, ter, X0,t6,t5,t5,t1,t2,t4,t1,t4,t4,t4,t5,t5,t1,X0)
    call wwRellenarFila(11, ter, X0,t6,t6,t5,t5,t1,t4,t1,t4,t4,t5,t5,t6,t1,X0)
    call wwRellenarFila(12, ter, X0,X0,t6,t6,t4,t4,t5,t5,t5,t5,t1,t6,t1,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t6,t6,t5,t5,t5,t5,t5,t6,t6,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t6,t6,t6,t6,t6,t6,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t6,X0,X0,X0,X0,X0,X0,X0)

    //TERRENO 5 (CIUDAD)   
    set t1 = 'Yblm'//
    set t2 = 'Ybtl'//
    set t3 = 'Yrtl'// 
    set t4 = 'Ywmb'//
    set ter = 5
    call SaveInteger(udg_wwT, 0, ter, 'LRma') //Rayos de luna
    //...........................01.02.03.04.05.06.07.08.09.10.11.12.13.14.15 
    call wwRellenarFila( 1, ter, X0,X0,X0,X0,X0,X0,X0,t4,X0,X0,X0,X0,X0,X0,X0)
    call wwRellenarFila( 2, ter, X0,X0,X0,X0,t1,t2,t1,t4,t1,t2,t1,X0,X0,X0,X0)
    call wwRellenarFila( 3, ter, X0,X0,X0,t1,t3,t1,t3,t4,t3,t1,t3,t1,X0,X0,X0)
    call wwRellenarFila( 4, ter, X0,X0,t1,t2,t1,t2,t1,t4,t1,t2,t1,t2,t1,X0,X0)
    call wwRellenarFila( 5, ter, X0,t1,t3,t1,t3,t1,t3,t4,t3,t1,t3,t1,t3,t1,X0)
    call wwRellenarFila( 6, ter, X0,t2,t1,t2,t1,t2,t1,t4,t1,t2,t1,t2,t1,t2,X0)
    call wwRellenarFila( 7, ter, X0,t1,t3,t1,t3,t1,t3,t4,t3,t1,t3,t1,t3,t1,X0)
    call wwRellenarFila( 8, ter, t4,t4,t4,t4,t4,t4,t4,t1,t4,t4,t4,t4,t4,t4,t4)
    call wwRellenarFila( 9, ter, X0,t1,t3,t1,t3,t1,t3,t4,t3,t1,t3,t1,t3,t1,X0)
    call wwRellenarFila(10, ter, X0,t2,t1,t2,t1,t2,t1,t4,t1,t2,t1,t2,t1,t2,X0)
    call wwRellenarFila(11, ter, X0,t1,t3,t1,t3,t1,t3,t4,t3,t1,t3,t1,t3,t1,X0)
    call wwRellenarFila(12, ter, X0,X0,t1,t2,t1,t2,t1,t4,t1,t2,t1,t2,t1,X0,X0)
    call wwRellenarFila(13, ter, X0,X0,X0,t1,t3,t1,t3,t4,t3,t1,t3,t1,X0,X0,X0)
    call wwRellenarFila(14, ter, X0,X0,X0,X0,t1,t2,t1,t4,t1,t2,t1,X0,X0,X0,X0)
    call wwRellenarFila(15, ter, X0,X0,X0,X0,X0,X0,X0,t4,X0,X0,X0,X0,X0,X0,X0)
endfunction

function wwCambiarTerreno takes integer tipo returns nothing
    local real x = GetRectCenterX(gg_rct_TerrenoPunto) - 128.0
    local real y = GetRectCenterY(gg_rct_TerrenoPunto) + 128.0
    local integer i = 1
    local integer j
    local integer ter = tipo * 15
    local integer aux
    loop
    exitwhen i > 15
        set j = 1
        loop
        exitwhen j > 15
            set aux = LoadInteger(udg_wwT, i + ter, j )
            if aux != 0 then
                call SetTerrainType(x + I2R(128 * j), y - I2R(128 * i), aux, -1, 1, 0)
            endif
            set j = j + 1
        endloop
        set i = i + 1
    endloop
    call RemoveWeatherEffect(udg_wwMeteo)
    set udg_wwMeteo = AddWeatherEffect( bj_mapInitialPlayableArea, LoadInteger(udg_wwT, 0, tipo) )
    call EnableWeatherEffect( udg_wwMeteo, true )
endfunction
//===========================================================================

//Arena
function wwControl takes nothing returns nothing
    local unit u
    local real xu
    local real yu
    local real xc = GetLocationX(udg_Centro)
    local real yc = GetLocationY(udg_Centro)
    local real rc
    local real radio
    call GroupEnumUnitsInRange(udg_wwG, xc, yc, 1100.0, null)
    loop
        set u = FirstOfGroup(udg_wwG)
    exitwhen u == null
        set radio = RMinBJ(RMaxBJ(I2R(GetUnitPointValue(u)),50.0),200.0) / 4.0
        call GroupRemoveUnit(udg_wwG, u)
        set xu = GetUnitX(u) - xc
        set yu = GetUnitY(u) - yc
        set rc = (xu*xu + yu*yu)
        if rc > ((855-radio)*(855-radio)) then
            set rc = SquareRoot(rc)
            call SetUnitX(u, (855-radio) * (xu / rc) + xc)
            call SetUnitY(u, (855-radio) * (yu / rc) + yc)
        endif
    endloop
endfunction

function wwCombate takes nothing returns nothing
    set udg_Heroe = CreateUnit(udg_JUGADOR, 'H000', GetLocationX(udg_Centro) + 600.0, GetLocationY(udg_Centro), 180.0)
    if (GetLocalPlayer() == udg_JUGADOR) then
        call SetCameraTargetController(udg_Heroe, 0.0, 0.0, false)
    endif
    call SetHeroLevel(udg_Heroe, 5, false)
    call UnitAddItem(udg_Heroe, CreateItem('phea', GetUnitX(udg_Heroe), GetUnitY(udg_Heroe)))
    call UnitAddItem(udg_Heroe, CreateItem('pman', GetUnitX(udg_Heroe), GetUnitY(udg_Heroe)))
    call UnitAddItem(udg_Heroe, CreateItem('phea', GetUnitX(udg_Heroe), GetUnitY(udg_Heroe)))
    call UnitAddItem(udg_Heroe, CreateItem('pman', GetUnitX(udg_Heroe), GetUnitY(udg_Heroe)))
    call UnitAddItem(udg_Heroe, CreateItem('phea', GetUnitX(udg_Heroe), GetUnitY(udg_Heroe)))
    call PauseUnit(udg_Heroe, true)
    call wwCambiarTerreno(udg_wwTerreno)
    call TriggerSleepAction(2.25)
    call PauseUnit(udg_Heroe, false)
    call AbortCinematicFadeBJ()
    call CinematicFadeCommonBJ(0.0, 0.0, 0.0, 1.0, "ReplaceableTextures\\CameraMasks\\White_mask.blp", 0, 100)
    call FinishCinematicFadeAfterBJ(1.0)
endfunction
//===========================================================================

//Inicio
function INI takes nothing returns nothing
    local real i = bj_PI/90.0
    local integer j = 1
    local real x
    local real y
    local trigger t = CreateTrigger(  )
        call AbortCinematicFadeBJ()
        call CinematicFadeCommonBJ(0.0, 0.0, 0.0, 0.0, "ReplaceableTextures\\CameraMasks\\White_mask.blp", 100, 0)
    call SetFloatGameState(GAME_STATE_TIME_OF_DAY, bj_MELEE_STARTING_TOD)
    call SetGameSpeed( MAP_SPEED_FASTEST )
    call SetMapFlag(MAP_LOCK_SPEED, true)
    call SetFloatGameState(GAME_STATE_TIME_OF_DAY, 12.00)
    call SetTimeOfDayScale(0.0)
    call SuspendTimeOfDay(true)
    call FogMaskEnable(false)
    call SetSkyModel( "Environment\\Sky\\FoggedSky\\FoggedSky.mdl" )
    set x = GetRectCenterX(gg_rct_Region_Central)
    set y = GetRectCenterY(gg_rct_Region_Central)
    set udg_Centro = Location(x, y)
    set udg_MaxRadio = 830.00 + 75.0
    loop
        exitwhen j > 180
        call CreateDestructable('B000', x + udg_MaxRadio * Cos(I2R(j) * i), y + udg_MaxRadio * Sin(I2R(j) * i), I2R(j*2+40), ( 0.75 + ( I2R(ModuloInteger(j, 2)) * 0.02 ) ), 0)
        set j = j + 1
    endloop
    set udg_MaxRadio = 830.00 
    call FogModifierStart(CreateFogModifierRadiusLoc(udg_JUGADOR, FOG_OF_WAR_VISIBLE, udg_Centro, 2000.00, true, false))
    call FogModifierStart(CreateFogModifierRadiusLoc(udg_JUGBOSS, FOG_OF_WAR_VISIBLE, udg_Centro, 2000.00, true, false))
    set udg_wwT = InitHashtable()
    set udg_wwS = InitHashtable()

    call wwTerrenos()
    set udg_wwG = CreateGroup()
    call TriggerRegisterTimerEvent(t, 0.025, true)
    call TriggerAddAction(t, function wwControl)
    call SP1ini()
    call SP2ini()
    call SP3ini()
    call SPDum2ini()
    call wwCombate()
    set t = null
endfunction
//***************************************************************************
//*
//*  Triggers
//*
//***************************************************************************

//===========================================================================
// Trigger: INICIO
//
// __ 0  --> Arena
// __ 1  --> Hierba
// __ 2  --> Corona de Hielo
// __ 3  --> Infierno
// __ 4  --> Veneno
// __ 5  --> Ciudad
//===========================================================================
function Trig_INICIO_Actions takes nothing returns nothing
    // La variable wwTerreno configura el terreno de la arena
    // Por defecto el terreno usado es 5 que corresponde a "Ciudad"
    // Pero cambiandole valor inicial se pueden elegir otros terrenos
    set udg_wwTerreno = 3
    // __ 0  --> Arena
    // __ 1  --> Hierba
    // __ 2  --> Corona de Hielo
    // __ 3  --> Infierno
    // __ 4  --> Veneno
    // __ 5  --> Ciudad
    // ------
    // ------
    // ------
    // ------
    // ------
    // ------
    call CreateQuestBJ( bj_QUESTTYPE_REQ_DISCOVERED, "TRIGSTR_1884", "TRIGSTR_1885", "ReplaceableTextures\\CommandButtons\\BTNMGexchange.tga" )
    call INI()
endfunction

//===========================================================================
function InitTrig_INICIO takes nothing returns nothing
    set gg_trg_INICIO = CreateTrigger(  )
    call TriggerAddAction( gg_trg_INICIO, function Trig_INICIO_Actions )
endfunction

//===========================================================================
function InitCustomTriggers takes nothing returns nothing
    call InitTrig_INICIO(  )
endfunction

//===========================================================================
function RunInitializationTriggers takes nothing returns nothing
    call ConditionalTriggerExecute( gg_trg_INICIO )
endfunction

//***************************************************************************
//*
//*  Players
//*
//***************************************************************************

function InitCustomPlayerSlots takes nothing returns nothing

    // Player 0
    call SetPlayerStartLocation( Player(0), 0 )
    call ForcePlayerStartLocation( Player(0), 0 )
    call SetPlayerColor( Player(0), ConvertPlayerColor(0) )
    call SetPlayerRacePreference( Player(0), RACE_PREF_HUMAN )
    call SetPlayerRaceSelectable( Player(0), false )
    call SetPlayerController( Player(0), MAP_CONTROL_USER )

    // Player 11
    call SetPlayerStartLocation( Player(11), 1 )
    call ForcePlayerStartLocation( Player(11), 1 )
    call SetPlayerColor( Player(11), ConvertPlayerColor(11) )
    call SetPlayerRacePreference( Player(11), RACE_PREF_NIGHTELF )
    call SetPlayerRaceSelectable( Player(11), false )
    call SetPlayerController( Player(11), MAP_CONTROL_COMPUTER )

endfunction

function InitCustomTeams takes nothing returns nothing
    // Force: TRIGSTR_086
    call SetPlayerTeam( Player(0), 0 )

    // Force: TRIGSTR_1772
    call SetPlayerTeam( Player(11), 1 )

endfunction

//***************************************************************************
//*
//*  Main Initialization
//*
//***************************************************************************

//===========================================================================
function main takes nothing returns nothing
    call SetCameraBounds( -4096.0 + GetCameraMargin(CAMERA_MARGIN_LEFT), -4096.0 + GetCameraMargin(CAMERA_MARGIN_BOTTOM), 4096.0 - GetCameraMargin(CAMERA_MARGIN_RIGHT), 4096.0 - GetCameraMargin(CAMERA_MARGIN_TOP), -4096.0 + GetCameraMargin(CAMERA_MARGIN_LEFT), 4096.0 - GetCameraMargin(CAMERA_MARGIN_TOP), 4096.0 - GetCameraMargin(CAMERA_MARGIN_RIGHT), -4096.0 + GetCameraMargin(CAMERA_MARGIN_BOTTOM) )
    call SetDayNightModels( "Environment\\DNC\\DNCLordaeron\\DNCLordaeronTerrain\\DNCLordaeronTerrain.mdl", "Environment\\DNC\\DNCLordaeron\\DNCLordaeronUnit\\DNCLordaeronUnit.mdl" )
    call SetTerrainFogEx( 0, 1200.0, 3000.0, 0.500, 0.000, 0.000, 0.502 )
    call SetWaterBaseColor( 255, 0, 0, 255 )
    call NewSoundEnvironment( "Default" )
    call SetAmbientDaySound( "SunkenRuinsDay" )
    call SetAmbientNightSound( "SunkenRuinsNight" )
    call SetMapMusic( "Music", true, 0 )
    call CreateRegions(  )
    call CreateCameras(  )
    call InitBlizzard(  )
    call InitGlobals(  )
    call InitCustomTriggers(  )
    call RunInitializationTriggers(  )

endfunction

//***************************************************************************
//*
//*  Map Configuration
//*
//***************************************************************************

function config takes nothing returns nothing
    call SetMapName( "TRIGSTR_025" )
    call SetMapDescription( "TRIGSTR_027" )
    call SetPlayers( 2 )
    call SetTeams( 2 )
    call SetGamePlacement( MAP_PLACEMENT_USE_MAP_SETTINGS )

    call DefineStartLocation( 0, 0.0, -256.0 )
    call DefineStartLocation( 1, 0.0, -256.0 )

    // Player setup
    call InitCustomPlayerSlots(  )
    call InitCustomTeams(  )
endfunction

