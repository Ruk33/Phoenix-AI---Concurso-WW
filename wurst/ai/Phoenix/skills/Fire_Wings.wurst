package Fire_Wings

import Vectors
import Angle
import TimedLoop

constant int RAW_CODE = 'A000'
public constant real fire_Wings_CASTING_TIME = 1.

class Fire_Wings
	use TimedLoop
	
	unit caster
	real castX
	real castY
	
	ondestroy
		this.caster = null
	
	override function onTimedLoop()
		vec2 casterLoc = vec2(this.caster.getX(), this.caster.getY())
		vec2 castLoc = vec2(this.castX, this.castY)
		real distance = castLoc.distToVec(casterLoc)
		
		if ( distance > 100 )
			angle newAngle = casterLoc.angleTo(castLoc)
			vec2 newLoc = vec2(polarProjectionX(casterLoc.x, distance / 30, newAngle), polarProjectionY(casterLoc.y, distance / 30, newAngle))
			
			this.caster.setFacing(newAngle)
			this.caster.setPos(newLoc)
		else
			this.stopTimedLoop()
	
	construct(unit caster, real castX, real castY)
		this.caster = caster
		this.castX = castX
		this.castY = castY
		
		this.startTimedLoop()

function onCast()	
	if ( GetSpellAbilityId() == RAW_CODE )
		new Fire_Wings(GetTriggerUnit(), GetSpellTargetX(), GetSpellTargetY())

init
	trigger onCastTrigger = CreateTrigger()
	
	onCastTrigger.registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT)
	onCastTrigger.addAction(function onCast)
	
	onCastTrigger = null